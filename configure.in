dnl Process this file with autoconf to produce a configure script.
AC_INIT(webalizer.c)

dnl default language (don't change!)
DEFAULT_LANG="english"
AC_SUBST(DEFAULT_LANG)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_INSTALL
AC_C_CHAR_UNSIGNED

dnl Check for command line arguments
AC_ARG_WITH(gdlib,--with-gdlib=DIR          Where to additionally look for libgd.a,
EXTRA_GDLIB="${withval}", EXTRA_GDLIB="")
AC_ARG_WITH(gd,--with-gd=DIR             Where to additionally look for gd headers,
EXTRA_GD="${withval}", EXTRA_GD="")

dnl force our own CFLAGS defaults
if test "$GCC" = yes; then
  CFLAGS="-Wall -O2"
  if test "$ac_cv_c_char_unsigned" = "yes"; then
   CFLAGS="${CFLAGS} -fsigned-char"
  fi
else
  CFLAGS="-g"
fi

dnl Check for bsd 4.4 library -l44bsd:
AC_CHECK_LIB(44bsd, main, LIBS="-l44bsd ${LIBS}")

dnl Check for math library -lm
AC_CHECK_LIB(m, main, HAVE_LIBM="1"; LIBS="-lm ${LIBS}")

dnl check for gd graphics library -lgd
AC_CHECK_LIB(gd, main,LIBS="-lgd ${LIBS}"; HAVE_LIBGD="yes", HAVE_LIBGD="")
if test "$HAVE_LIBGD" = ""; then
 AC_FIND_PATH(GDLOC,libgd.a,"", ${PREFIX}/lib ${PREFIX}/lib/gd /usr/local/lib /usr/local/lib/gd /usr/local/gd /usr/local/include /usr/local/include/gd /usr/lib/gd /usr/lib /lib ./ ./gd ./gd1.2 ./gd1.3 ${EXTRA_GDLIB} ${EXTRA_GD})
 if test "$GDLOC" = ""; then
  AC_MSG_ERROR(gd library not found... please install gd.)
 else
  LIBS="-lgd ${LIBS}"
  LDFLAGS="-L${GDLOC} ${LDFLAGS}"
 fi
fi

dnl find path to GD library headers
AC_FIND_PATH(GDLIB,gd.h,"",${PREFIX}/include ${PREFIX}/include/gd /usr/local/include/gd /usr/local/include /usr/include/gd /usr/include /include/gd /include ./gd ./gd1.2 ./gd1.3 . ${EXTRA_GD} ${EXTRA_GDLIB})
dnl Abort if GD headers not found
if test "$GDLIB" = ""; then
AC_MSG_ERROR(gd headers not found... please install gd.)
fi
LANG_CACHE=yes
AC_ARG_WITH(language,--with-language=language  Use 'language' (default is english),
WEBALIZER_LANG="${withval}"; LANG_CACHE=no, LANG_CACHE=yes)
if test "$WEBALIZER_LANG" = "no"; then WEBALIZER_LANG=english; fi
dnl
dnl check for valid language file
dnl
AC_MSG_CHECKING(for language file)
if test "$LANG_CACHE" = "yes"; then
AC_CACHE_VAL(ac_cv_language, ac_cv_language=$DEFAULT_LANG)
WEBALIZER_LANG=$ac_cv_language
fi
if test -f lang/webalizer_lang.${WEBALIZER_LANG}; then
AC_MSG_RESULT(yes - ${WEBALIZER_LANG})
else
 if test -f lang/webalizer_lang.${DEFAULT_LANG}; then
  AC_MSG_RESULT('${WEBALIZER_LANG}' not found - using ${DEFAULT_LANG})
  WEBALIZER_LANG=${DEFAULT_LANG}
 else
  AC_MSG_RESULT(no)
  AC_MSG_ERROR(language files not found... fix before continuing)
 fi
fi
ac_cv_language=${WEBALIZER_LANG}

dnl additonal platform specific checks
AC_CHECK_HEADERS(getopt.h)
if test "$HAVE_LIBM" = "1"; then
 AC_CHECK_HEADERS(math.h)
fi

dnl create link to language file
AC_LINK_FILES(lang/webalizer_lang.${WEBALIZER_LANG}, webalizer_lang.h)

dnl the stuff below was automatically generated by autoscan...
dnl since we don't (yet) have conditionals in our code to handle these
dnl situations, we just ignore them for now...

dnl Checks for header files.
dnl AC_HEADER_STDC
dnl AC_CHECK_HEADERS(unistd.h)
dnl Checks for typedefs, structures, and compiler characteristics.
dnl AC_STRUCT_TM
dnl Checks for library functions.
dnl AC_FUNC_STRFTIME
dnl AC_CHECK_FUNCS(strstr strtoul uname)

AC_OUTPUT(Makefile)
